plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'au.com.dius.pact' version '4.3.6'
	id 'org.unbroken-dome.test-sets' version '4.0.0'
}

group = 'com.nathandeamer'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2021.0.1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation "com.google.guava:guava:31.0.1-jre"

	testImplementation 'com.github.tomakehurst:wiremock:2.27.2'
	testImplementation 'com.atlassian.ta:wiremock-pact-generator:2.4.3'

	testImplementation 'au.com.dius.pact.consumer:junit5:4.3.6'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

def getGitBranch = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

static def getOrDefault(env, defaultVal) {
	def val = System.getenv(env)
	if (val == null || val.isEmpty()) {
		val = defaultVal
	}
	return val
}

testSets {
	pactTest {
		dirName = 'pact-test'
	}
}

pactTest {
	useJUnitPlatform()
	filter {
		setFailOnNoMatchingTests(false) // There won't always be consumer tests or provider tests
	}

	if (System.getProperty('pactPublishResults') == 'true') {
		systemProperty 'pact.provider.version', getGitHash()
		systemProperty 'pact.provider.branch', getGitBranch()
		systemProperty 'pact.verifier.publishResults', 'true'
	}
}

test {
	useJUnitPlatform()
}

//pact {
//	broker {
//		pactBrokerUrl = getOrDefault("PACT_BROKER_BASE_URL", null)
//		pactBrokerToken = getOrDefault("PACT_BROKER_TOKEN", null)
//	}
//
//	publish {
//		consumerVersion = getGitHash()
//		consumerBranch = getGitBranch()
//		//tags = [getGitBranch()]
//	}
//
//}